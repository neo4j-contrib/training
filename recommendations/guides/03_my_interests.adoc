= Recommendations by explicit interest
include::include.adoc[]

== Member Interests (Topics)

What data do we have?
Run the following query to find out:

[source,cypher,subs=attributes]
----
LOAD CSV WITH HEADERS
FROM "{csv-url}members.csv" AS row
RETURN row.id, row.topics
LIMIT 10
----

Note that we're still processing the same members file as we were earlier but returning different columns this time.

So we've got one column containing the member id and one with a `;` delimited set of topic ids.
Our next task is to create a relationship between members and their topics.
To do that we're going to need to split up the string containing topic ids so we can process them one at a time.

We'll use link:http://neo4j.com/docs/new-manual/developer-manual/3.0-RC1/#functions-split[split()] and link:http://neo4j.com/docs/new-manual/developer-manual/3.0-RC1/#query-plan-unwind[UNWIND] to help us do this.

ifdef::env-training[]

== The slides are waiting...for you!

image::{img}/slides.jpg[]

endif::env-training[]

== Member Interests (Topics)

Run the following query to create an `INTERESTED_IN` relationship between members and topics:

[source,cypher,subs=attributes]
----
USING PERIODIC COMMIT 10000
LOAD CSV WITH HEADERS FROM "{csv-url}members.csv" AS row

WITH split(row.topics, ";") AS topics, row.id AS memberId
UNWIND topics AS topicId

WITH DISTINCT memberId, topicId
MATCH (member:Member {id: memberId})
MATCH (topic:Topic {id: topicId})
MERGE (member)-[:INTERESTED_IN]->(topic)
----

This might take a bit of time - there's a lot of members and topics to get through!

== Find my similar groups

Now we're ready to write our first query which uses some data we've explicitly provided meetup.com.

include::form.adoc[tags=name]

[source,cypher,subs="attributes,none"]
----
MATCH (member:Member {name: {name}})-[:INTERESTED_IN]->(topic),
      (member)-[:MEMBER_OF]->(group)-[:HAS_TOPIC]->(topic)
WITH member, topic, COUNT(*) AS score
MATCH (topic)<-[:HAS_TOPIC]-(otherGroup)
WHERE NOT (member)-[:MEMBER_OF]->(otherGroup)
RETURN otherGroup.name, COLLECT(topic.name), SUM(score) as score
ORDER BY score DESC
LIMIT 10
----

In this query we

  * start from a user
  * find the topics they are interested in
  * find the groups they are a member of that have those topics
  * aggregates the number of times each topic appears
  * finds other groups that have these topics, filtering out ones that they are already a member of
  * returns those other groups sorted by how many of the member's topics they cater for

This is quite a crude scoring mechanism which favours generally popular topics.
We'll have a look at how to address that later on.

== What if I don't specify any interests?

Let's find someone who didn't specify any interests but is member of a few groups.

[source,cypher]
----
MATCH (member:Member) WHERE NOT (member)-[:INTERESTED_IN]->()
MATCH (member)-[:MEMBER_OF]->()
RETURN member.name, count(*) as groups
ORDER BY count(*) DESC
LIMIT 50
----

Let's take someone from that list who didn't specify any interests when they signed up for meetup.com:

include::form.adoc[tags=name]

[source,cypher,subs="attributes,none"]
----
MATCH (member:Member {name: {name}})-[:INTERESTED_IN]->(topic), (member)-[:MEMBER_OF]->(group)-[:HAS_TOPIC]->(topic)
WITH member, topic, COUNT(*) AS score
MATCH (topic)<-[:HAS_TOPIC]-(otherGroup)
WHERE NOT (member)-[:MEMBER_OF]->(otherGroup)
RETURN otherGroup.name, COLLECT(topic.name), SUM(score) as score
ORDER BY score DESC
----

So we can't make any recommendations to them.
Sad times!

However, if they are a member of some groups we can infer their interests.

== What am I actually interested in?

The `INTERESTED_IN` relationships that we created are based on interests that a user declared when they signed up for meetup.com.
People typically don't change those as their interests change but they do join new groups frequently which allows us to know their interests better than they do!

We can do this by inferring `INTERESTED_IN` relationships based on these groups.

image::{img}/group_has_topic_member_of.png[]

ifdef::env-training[]

== Exercise: Inferring interests

* Find topics that I'm interested in via the groups I'm a member of that I don't have an `INTERESTED_IN` relationship to.
* Which topic shows up most frequently?

== The answers are waiting...

image::{img}/slides.jpg[]

endif::env-training[]

== Persisting inferred interests

We can write the following query to add inferred `INTERESTED_IN` relationships to each member:

[source,cypher]
----
MATCH (m:Member)-[:MEMBER_OF]->()-[:HAS_TOPIC]->(topic)
WHERE NOT (m)-[:INTERESTED_IN]->(topic)

WITH m, topic, COUNT(*) AS times
WHERE times >= 3

MERGE (m)-[interestedIn:INTERESTED_IN]->(topic)
SET interestedIn.inferred = true
----

The query:

* goes over each member
* finds the groups they're a member of and which topics those groups advertise
* counts how many times each topic has showed up
* removes any topics that show up less than 3 times
* creates an `INTERESTED_IN` relationship between the member and the remaining topics

On this dataset we have a sufficiently small number of members that we can process them all in one go.
With a production sized dataset we'd need to process members in batches.

== Find my similar groups (even if I don't know it yet)

So we're still answering the same fundamental question as before:

[verse]
____
As a member of several meetup groups
I want to find other similar meetup groups that Iâ€™m not already a member of
So that I can join those groups
____

But now we've added in some extra relationships which allow us to make recommendations even to people like Aleks who haven't specified any interests.

Run the following query:

include::form.adoc[tags=name]

++++
<label>Name:</label> <input value-for="name" class="form-control" value="Mark Needham">
++++

[source,cypher,subs="attributes,none"]
----
MATCH (member:Member)-[:INTERESTED_IN]->(topic),
      (member)-[:MEMBER_OF]->(group)-[:HAS_TOPIC]->(topic)
WHERE member.name CONTAINS {name}
WITH member, topic, COUNT(*) AS score
MATCH (topic)<-[:HAS_TOPIC]-(otherGroup)
WHERE NOT (member)-[:MEMBER_OF]->(otherGroup)
RETURN otherGroup.name, COLLECT(topic.name), SUM(score) as score
ORDER BY score DESC
----

Hopefully you can now see some results where previously there weren't any.

== Next Step

We're now reached the end of the group recommendation section of the training.
In the next section we're going to explore the events hosted by our groups and then make event recommendations.

pass:a[<a play-topic='{guides}/04_events.html'>Event Recommendations</a>]
